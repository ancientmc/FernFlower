apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

archivesBaseName = 'fernflower'
group = 'com.ancientmc'
version = '1.0'
ext {
    CHANGELOG = rootProject.file('build/changelog.txt')
}
 
sourceSets {
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['test']
}

repositories {
    mavenCentral()
    maven { url = "https://libraries.minecraft.net/" }
    maven { url = 'https://maven.minecraftforge.net/' }
}

dependencies {
    testImplementation 'junit:junit:4.+'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.assertj:assertj-core:3.+'
    testImplementation 'com.google.code.gson:gson:2.8.0'
    testImplementation 'net.minecraftforge:DiffPatch:2.0.5:all'
}

java.toolchain {
    languageVersion = JavaLanguageVersion.of(8)
}

// Default all standard Java compile tasks to Java 8
tasks.withType(JavaCompile) {
    options.deprecation = true
}

eclipse {
    classpath {
        sourceSets
    }
    project {
        name 'ForgeFlower'
    }
}

jar {
    manifest {
        attributes(
            'Main-Class' : 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler',
            'Multi-Release': 'true'
        )
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives sourceJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            if (CHANGELOG.exists()) {
                artifact source: CHANGELOG, classifier: 'changelog'
            }
            artifact tasks.sourceJar

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                name = project.archivesBaseName
                packaging = 'jar'
                description = 'Fernflower from https://github.com/JetBrains/intellij-community as standalone library, With Forge\'s Modifications, further modified by RetroGradle and AncientMC.'
                url = 'https://github.com/ancientmc/FernFlower'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ancientmc-maven'
            url = 'D:\\Programs\\Projects\\AncientMC\\ancientmc-maven\\'
        }
    }
}
